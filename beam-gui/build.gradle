apply plugin: 'application'
mainClassName = "beam.gui.Gui"
compileJava.options.encoding = 'UTF-8'

dependencies {
    compile(project(":"))
    compile 'com.typesafe:config:1.3.0'
}

distributions {
    main {
        contents {
            from('..') {
                include 'LICENSE', 'README.md'
            }
            into('input') {
                from('../test/input') {
                    include 'beamville/**', 'sf-light/**', 'common/**', 'dtd/**'
                    exclude '**/output/**'
                }
            }
        }
    }
}

// from https://issues.gradle.org/browse/GRADLE-2992
// Fix command-length issue in windows startscript
task pathingJar(type: Jar) {
    appendix = 'pathing'
    manifest { attributes("Class-Path": configurations.runtime.collect { it.getName() }.join(' ') + ' ' + jar.archiveName ) }
}
applicationDistribution.from(pathingJar) { into "lib" }
startScripts {
    doLast {
        def winScriptFile  = file getWindowsScript()
        def winFileText = winScriptFile.text

        // Remove too-long-classpath and use pathing jar instead
        winFileText = winFileText.replaceAll('set CLASSPATH=.*', 'rem CLASSPATH declaration removed.')
        winFileText = winFileText.replaceAll('("%JAVA_EXE%" .* -classpath ")%CLASSPATH%(" .*)', '$1%APP_HOME%\\\\lib\\\\' + pathingJar.archiveName + '$2')

        winScriptFile.text = winFileText
    }
}