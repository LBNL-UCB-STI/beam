import com.amazonaws.services.lambda.model.InvocationType
import jp.classmethod.aws.gradle.lambda.AWSLambdaInvokeTask

buildscript {
    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
        classpath group: 'kr.motd.gradle', name: 'sphinx-gradle-plugin', version: '1.0.3.Final'
        classpath "jp.classmethod.aws:gradle-aws-plugin:0.35"
    }
}

plugins {
    id "net.ltgt.apt" version "0.5"
    id "de.undercouch.download" version "3.2.0"
}

configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 86400, 'seconds'
}

configurations {
    // Depending on which binding you want to use for slf4j, use these to exclude all others
//    runtime.exclude group: "org.slf4j", module: "slf4j-log4j12"
    runtime.exclude group: 'org.slf4j', module: 'slf4j-simple'
    runtime.exclude group: 'ch.qos.logback', module: 'logback-classic'
}

//ext {
//    env = "beamville"
//}

apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'kr.motd.sphinx'
apply plugin: 'application'

group = 'beam'
version = '0.2.0-SNAPSHOT'

description = """"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

def scalaBinaryVersion = "2.12"
def akkaBinaryVersion = "2.4.16"
def circeBinaryVersion="0.7.1"
def slf4jVersion = "1.7.12"

sourceSets.main.scala.srcDirs = ["src/main/scala", "src/main/java"]
sourceSets.main.java.srcDirs = []

sourceSets.test.java.srcDirs = ["src/test/java"]
sourceSets.test.scala.srcDirs = ["src/test/scala"]

if (project.hasProperty('env')) {
    sourceSets {
        main {
            resources {
                srcDirs "src/main/resources", "test/input/" + project.getProperty('env')
            }
        }
    }
}


repositories {
    maven { url "http://maven.geotoolkit.org/" }
    maven { url "https://repository.jboss.org/nexus/content/repositories/thirdparty-releases" }
    maven { url "http://central.maven.org/maven2" }
    maven { url "http://repo.maven.apache.org/maven2" }
    maven { url "http://download.osgeo.org/webdav/geotools" }
    maven { url "http://dl.bintray.com/matsim/matsim" }
    maven { url "http://maven.conveyal.com/" }
    maven { url "http://repo1.maven.org/maven2" }
    maven { url "http://download.java.net/maven/2/" }
    maven { url "http://people.apache.org/repo/m1-ibiblio-rsync-repository/org.apache.axis2/" }
    maven { url "http://dl.bintray.com/andimarek/graphql-java" }
    maven { url "http://maven.geo-solutions.it" }
    maven { url "http://dl.bintray.com/scalaz/releases" }
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url "http://nexus.onebusaway.org/content/groups/public/" }
    maven { url "https://jitpack.io" }
}

dependencies {

    ////////////////////////////
    // Java dependencies
    ////////////////////////////

    compile group: 'com.google.inject', name: 'guice', version: '4.1.0'
    compile group: 'com.google.inject.extensions', name: 'guice-assistedinject', version: '4.1.0'
    compile group: 'com.google.inject.extensions', name: 'guice-multibindings', version: '4.1.0'
    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    compile group: 'de.ruedigermoeller', name: 'fst', version: '2.47'
    compile group: 'org.jfree', name: 'jfreechart', version: '1.0.14'
    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.1'
    compile group: 'org.apache.commons', name: 'commons-math3', version: '3.5'
    compile group: 'org.geotools', name: 'gt-main', version: '13.0'
    compile group: 'org.geotools', name: 'gt-referencing', version: '13.0'
    compile group: 'org.geotools', name: 'gt-shapefile', version: '13.0'
    compile group: 'net.sf.trove4j', name: 'trove4j', version: '3.0.3'
    runtime group: 'org.geotools', name: 'gt-epsg-hsql', version: '13.0'
    compile group: 'aopalliance', name: 'aopalliance', version: '1.0'
    compile group: 'de.ruedigermoeller', name: 'fst', version: '2.48'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.5.5'
    compile group: 'org.javassist', name: 'javassist', version: '3.19.0-GA'
    compile group: 'javax.inject', name: 'javax.inject', version: '1'
    compile group: 'jdom', name: 'jdom', version: '1.1'
    compile group: 'org.jdom', name: 'jdom2', version: '2.0.5'
    compile group: 'log4j', name: 'log4j', version: '1.2.16'
    compile group: 'org.matsim.contrib', name: 'multimodal', version: '0.8.0'
    compile group: 'org.objenesis', name: 'objenesis', version: '2.4'
    compile group: 'javax.jms', name: 'jms', version: '1.1'
    compile group: 'javax.media', name: 'jai_core', version: '1.1.3'
    compile group: 'org.geotools', name: 'gt-referencing', version: '15.2'
    compile group: 'org.geotools', name: 'gt-epsg-hsql', version: '15.2'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile group: 'com.esotericsoftware', name: 'kryo-shaded', version: '4.0.0'
    compile 'net.sf.supercsv:super-csv:2.4.0'
    compile group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.2'
    compile group: 'edu.ucar', name: 'udunits', version: '4.5.5'
    //compile 'com.github.shevek:graphviz4j:master-SNAPSHOT'
    compile 'org.reflections:reflections:0.9.10'
    compile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.2-b01'
    compile group: 'com.github.colinsheppard.matsim_all', name: 'matsim', version: 'master-SNAPSHOT', changing: false
    compile group: 'com.github.colinsheppard', name: 'pt2matsim', version: 'master-SNAPSHOT', changing: false
//    compile group: 'com.conveyal', name: 'r5', version: '2.1.1'
    compile group: 'com.github.LBNL-UCB-STI', name: 'r5', version: 'beam-3.0-SNAPSHOT', changing: true


    testCompile group: 'junit', name: 'junit', version: '4.4'
    // https://mvnrepository.com/artifact/org.powermock/powermock-module-junit4
    testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '1.6.6'
    testCompile group: "org.mockito", name: "mockito-all", version: "1.9.5"


    /////////////////////////////////
    // Scala dependencies
    /////////////////////////////////

    // CORE Scala //
    compile "org.scala-lang:scala-library:${scalaBinaryVersion}"
    compile group: 'org.scala-lang.modules', name: "scala-xml_${scalaBinaryVersion}", version: '1.0.6'

    // NEEDED FOR USING REPL //
    compile "org.scala-lang:scala-compiler:${scalaBinaryVersion}.1"

    // TEST Scala //
    testCompile group: 'org.scalatest', name: "scalatest_${scalaBinaryVersion}", version: '3.0.1'

    // 3rd Party Scala //
    //    compile 'org.scalaz:scalaz-core_2.12:7.3.0-M9'
    compile "net.codingwell:scala-guice_${scalaBinaryVersion}:4.1.0"  // DI
    compile 'com.github.carueda:tscfg:v0.8.0'  // config
    // https://mvnrepository.com/artifact/org.scalaz/scalaz-core_2.12
    compile group: 'org.scalaz', name: "scalaz-core_${scalaBinaryVersion}", version: '7.3.0-M10'
    //compile group: 'com.lihaoyi', name: 'pprint', version: '0.4.3'
    //compile group: 'com.github.lihaoyi.upickle-pprint', name:'pprint', version:'0.4.0', changing: false
    // https://mvnrepository.com/artifact/com.beachape/enumeratum_2.12
    compile group: 'com.beachape', name: "enumeratum_${scalaBinaryVersion}", version: "1.5.12"
// https://mvnrepository.com/artifact/com.beachape/enumeratum-circe_2.12
    compile group: 'com.beachape', name: "enumeratum-circe_${scalaBinaryVersion}", version: "1.5.14"
    // https://mvnrepository.com/artifact/io.circe/circe-core_2.12
    compile group: 'io.circe', name: "circe-core_${scalaBinaryVersion}", version: circeBinaryVersion
    // https://mvnrepository.com/artifact/io.circe/circe-generic_2.12
    compile group: 'io.circe', name: "circe-generic_${scalaBinaryVersion}", version: circeBinaryVersion
    // https://mvnrepository.com/artifact/io.circe/circe-parser_2.12
    compile group: 'io.circe', name: "circe-parser_${scalaBinaryVersion}", version: circeBinaryVersion
    compile group: 'com.github.stephenc.eaio-uuid', name: "uuid", version: "3.4.0"

    /////////////
    // Akka Dependencies
    ////////////

    // CORE Akka //
    compile group: 'com.typesafe.akka', name: "akka-actor_${scalaBinaryVersion}", version: akkaBinaryVersion
//    compile group: 'com.typesafe.akka', name: "akka-persistence_${scalaBinaryVersion}", version: akkaBinaryVersion
//    compile group: 'com.typesafe.akka', name: "akka-remote_${scalaBinaryVersion}", version: akkaBinaryVersion
//    compile group: 'com.typesafe.akka', name: "akka-cluster_${scalaBinaryVersion}", version: akkaBinaryVersion
    compile group: 'com.typesafe.akka', name: "akka-contrib_${scalaBinaryVersion}", version: akkaBinaryVersion
//    compile group: 'org.iq80.leveldb', name: 'leveldb', version: '0.9'

    // TEST Akka //
    testCompile group: 'com.typesafe.akka', name: "akka-testkit_${scalaBinaryVersion}", version: akkaBinaryVersion

    // 3rd Party Akka //
    //compile group: 'org.iq80.leveldb', name: 'leveldb', version: '0.7'
//    compile group: 'org.fusesource.leveldbjni', name: 'leveldbjni-all', version: '1.8'
    //compile group: 'com.google.protobuf', name: 'protobuf-java', version: '2.5.0'
    compile "tv.cntt:glokka_${scalaBinaryVersion}:2.4.0"  // Actor registry

    compile "org.slf4j:slf4j-api:${slf4jVersion}"
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.9.0'

}

// Task to run scala tests, as Scala tests not picked up by Gradle by default.
task spec(dependsOn: ['testClasses'], type: JavaExec) {
    main = 'org.scalatest.tools.Runner'
    args = ['-R', 'build/classes/test', '-o']
    classpath = sourceSets.test.runtimeClasspath
}
test.dependsOn spec

//////////////////////////////////////////////////////////////////////
// BUNDLING FOR EXECUTION
//
// Shadowjar bundles model together into executable jar
//////////////////////////////////////////////////////////////////////


apply plugin: 'com.github.johnrengelman.shadow'

jar {
    manifest {
        attributes "Implementation-Title": project.name,
                "Implementation-Version": version,
                "Specification-Vendor": "The BEAM Team",
                "Implementation-Vendor": "The BEAM Team",
                'Main-Class': 'beam.sim.RunBeam'
    }
}

shadowJar {
    baseName = 'beam'
    zip64 true
    classifier = null
    version = null
}

assemble.dependsOn shadowJar

//////////////////////////////////////////////////////////////////////
// Amazon WS task to run beam sim on Lambda
//////////////////////////////////////////////////////////////////////

apply plugin: "base"
apply plugin: "jp.classmethod.aws.lambda"

aws {
    profileName = "gradle"
    region = "us-east-2"
}

lambda {
    region = "us-east-2"
}

def pload = """{
  "branch": "$beamBranch",
  "build": "$beamBuild",
  "input": "$beamInput",
  "configs": [ "${beamConfigs.split(',').join("\", \"")}" ],
  "shutdown_wait": "$shutdownWait"
}"""

task runAwsSim(type: AWSLambdaInvokeTask) {
    functionName = "testSim"
    invocationType = InvocationType.RequestResponse
    payload = pload

    doLast {
        println pload
        println "Lambda function result: " + new String(invokeResult.payload.array(), "UTF-8")
    }
}

//////////////////////////////////////////////////////////////////////
// Generate config classes reflecting the application.conf file
//////////////////////////////////////////////////////////////////////
task generateConfig {
    doLast {
        if (!project.file('build/tscfg-0.8.1.jar').exists()) {
            download {
                src 'https://github.com/carueda/tscfg/releases/download/v0.8.1/tscfg-0.8.1.jar'
                dest buildDir
            }
        }
        javaexec {
            main = "-jar"
            args = [
                    "build/tscfg-0.8.1.jar",
                    "--spec", "src/main/resources/beam-template.conf",
                    "--scala",
                    "--pn", "beam.sim.config",
                    "--cn", "BeamConfig",
                    "--dd", "src/main/scala/beam/sim/config/"
            ]
        }
    }
}

task repl(type: JavaExec) {
    main = "scala.tools.nsc.MainGenericRunner"
    classpath = sourceSets.main.runtimeClasspath
    standardInput System.in
    args '-usejavacp'
}

task deleteSf {
    doLast {
        if (project.file('production/application-sfbay/r5/network.dat').exists()) {
            delete 'production/application-sfbay/r5/network.dat'
        }
        if (project.file('production/application-sfbay/r5/osm.mapdb').exists()) {
            delete 'production/application-sfbay/r5/osm.mapdb'
        }
        if (project.file('production/application-sfbay/r5/osm.mapdb.p').exists()) {
            delete 'production/application-sfbay/r5/osm.mapdb.p'
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

//
// Run Via application plugin
// ./gradlew run -PappArgs="['--config', 'production/application-sfbay/beam.conf']"
//
mainClassName = "beam.sim.RunBeam"

def myAvailableRam = System.getenv("MAXRAM") ?: "16g"

// Use following for remote debug mode
//applicationDefaultJvmArgs = ["-Xmx${myAvailableRam}","-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=8005"]
applicationDefaultJvmArgs = ["-Xmx${myAvailableRam}"]

run {
    if (project.hasProperty("appArgs")) {
        args Eval.me(appArgs)
    }
}
